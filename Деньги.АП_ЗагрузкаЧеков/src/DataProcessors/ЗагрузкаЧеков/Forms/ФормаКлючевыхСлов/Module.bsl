
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтатьяРасходов = ЭтаФорма.Параметры.СтатьяРасходов;
	
	// Получаем список ключевых слов из регистра
	МассивКлючевыхСлов = ПолучитьСписокУстановленныхКлючевыхСлов(СтатьяРасходов);
	
	// Выводим слова в таблицу
	СписокСлов.Очистить();
	СписокСлов.ЗагрузитьЗначения(МассивКлючевыхСлов);
	СписокСлов.ЗаполнитьПометки(Истина);
	
	// Разбиваем текущее наименование по словам
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(ЭтаФорма.Параметры.НаименованиеПокупки, " .,;:#№/""");
	
	// Добавляем отсутствующие слова в список без пометки.
	Для Каждого Слово из МассивСлов Цикл
		КлСлово = НРег(Слово);
		УникальныйЭлемент = Истина;
		
		Для Каждого ЭлементМассива из МассивКлючевыхСлов Цикл
			Если ЭлементМассива = КлСлово тогда
				УникальныйЭлемент = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если УникальныйЭлемент тогда
			МассивКлючевыхСлов.Добавить(КлСлово);
			СписокСлов.Вставить(0,КлСлово,КлСлово, Ложь);
		КонецЕсли;
		
	КонецЦикла;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокУстановленныхКлючевыхСлов(СтатьяЗатрат)
	ИдентификаторОбъектаСтатьяРасходов = ОбщегоНазначения.ИдентификаторОбъектаМетаданных ("Справочник.СтатьиРасходов");
	
	НаборЗаписей = РегистрыСведений.КлючевыеСловаОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СтатьяЗатрат);
	НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(ИдентификаторОбъектаСтатьяРасходов);
	НаборЗаписей.Прочитать();
	МассивКлючевыхСлов = Новый Массив;
	
	Для Каждого Запись из НаборЗаписей Цикл
		МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(Запись.КлючевоеСлово, " ");
		Для каждого ТекСлово из МассивСлов Цикл
			КлСлово = НРег(Сред(ТекСлово,2));
			УникальныйЭлемент = Истина;
			Для Каждого ЭлементМассива из МассивКлючевыхСлов Цикл
				Если ЭлементМассива = КлСлово тогда
					УникальныйЭлемент = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если УникальныйЭлемент тогда
				МассивКлючевыхСлов.Добавить(КлСлово);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивКлючевыхСлов;
КонецФункции

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	ОбновитьРегистрНаСервере();
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаСервере
Процедура ОбновитьРегистрНаСервере()
	ИдентификаторОбъектаСтатьяРасходов = ОбщегоНазначения.ИдентификаторОбъектаМетаданных ("Справочник.СтатьиРасходов");
	СтрокаКлючевыхслов = "";
	
	СписокСлов.СортироватьПоПредставлению();
	Для каждого Элемент из СписокСлов Цикл
		Если Элемент.Пометка Тогда
			Если Не ПустаяСтрока(СтрокаКлючевыхслов) тогда
				СтрокаКлючевыхслов = СтрокаКлючевыхслов + " "
			КонецЕсли;
			СтрокаКлючевыхслов = СтрокаКлючевыхслов + "#" + Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей = РегистрыСведений.КлючевыеСловаОбъектов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(СтатьяРасходов);
	НаборЗаписей.Отбор.ИдентификаторОбъекта.Установить(ИдентификаторОбъектаСтатьяРасходов);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Запись = НаборЗаписей.Добавить();
	Запись.КлючевоеСлово = СтрокаКлючевыхслов;
	Запись.ИдентификаторОбъекта = ИдентификаторОбъектаСтатьяРасходов;
	Запись.Объект = СтатьяРасходов;
	
	НаборЗаписей.Записать();
КонецПроцедуры